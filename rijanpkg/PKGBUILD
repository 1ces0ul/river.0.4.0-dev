# Maintainer: Alex G <icesoul_mo@gmail.com>
pkgname=rijan-git
pkgver=fd08913
pkgrel=1
pkgdesc="An experimental Wayland compositor written in Janet and Zig"
arch=('x86_64' 'aarch64')
url="https://codeberg.org/ifreund/rijan"
license=('GPL-3.0-only')

# 确保系统中有这些开发库
depends=('wayland' 'libxkbcommon' 'pixman' 'libevdev' 'libinput' 'libffi')
makedepends=('zig>=0.15.0' 'wayland-protocols' 'scdoc' 'pkgconf' 'git')
provides=('rijan')
conflicts=('rijan')

source=(
  "rijan::git+https://codeberg.org/ifreund/rijan.git"
  "rijan.janet.patched"
  )
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "$srcdir/rijan"
  git describe --long --tags --always | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/rijan"

#######
  cp "$srcdir/rijan.janet.patched" "$srcdir/rijan/rijan.janet"
#######

  mkdir -p "$srcdir/zig-cache"
  mkdir -p "$srcdir/zig-local-cache"
}

build() {
  cd "$srcdir/rijan"

  export ZIG_GLOBAL_CACHE_DIR="$srcdir/zig-cache"
  export ZIG_LOCAL_CACHE_DIR="$srcdir/zig-local-cache"

  # 显式指定使用系统 libc
  zig build \
    -Doptimize=ReleaseSafe \
    -Dcpu=baseline \
    --summary all
  }

package() {
  cd "$srcdir/rijan"

  export ZIG_GLOBAL_CACHE_DIR="$srcdir/zig-cache"
  export ZIG_LOCAL_CACHE_DIR="$srcdir/zig-local-cache"

  DESTDIR="$pkgdir" zig build \
    -Doptimize=ReleaseSafe \
    -Dcpu=baseline \
    --prefix /usr \
    install

  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
